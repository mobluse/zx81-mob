   1 GOTO 10
   5 SAVE "CHAS%E"
  10 SCROLL
  11 PRINT TAB 13;"CHASE"
  20 SCROLL
  21 PRINT TAB 7;"CREATIVE COMPUTING"
  25 SCROLL
  26 PRINT TAB 5;"MORRISTOWN, NEW JERSEY"
  27 SCROLL
  28 SCROLL
  29 SCROLL
  41 SCROLL
  42 PRINT "YOU ARE WITHIN THE WALLS OF A"
  43 SCROLL
  44 PRINT "HIGH VOLTAGE MAZE"
  45 SCROLL
  46 PRINT "THERE ARE FIVE SECURITY MACHINES"
  47 SCROLL
  48 PRINT "TRYING TO DESTROY YOU"
  60 SCROLL
  61 PRINT "YOU ARE THE ""*"""
  62 SCROLL
  63 PRINT "THE INTERCEPTORS ARE THE ""+"""
  70 SCROLL
  71 PRINT "THE AREAS MARKED ""X"" ARE HIGH"
  72 SCROLL
  73 PRINT "VOLTAGE"
  80 SCROLL
  81 PRINT "YOUR ONLY CHANCE FOR SURVIVAL IS"
  82 SCROLL
  83 PRINT "TO MANEUVER EACH INTERCEPTOR"
  85 SCROLL
  86 PRINT "INTO AN ""X"".-----GOOD LUCK----"
  90 SCROLL
  92 POKE 16418,0
  93 PRINT AT 23,0;" - I AM THINKING, JUST WAIT..."
  99 GOTO 180
 100 PRINT AT 23,0;"                                "
 101 POKE 16418,2
 108 SCROLL
 109 PRINT "MOVES ARE    7.8.9"
 110 SCROLL
 111 PRINT "             4.*.6"
 120 SCROLL
 121 PRINT "             1.2.3"
 130 SCROLL
 140 SCROLL
 141 PRINT "10 = NO MOVE FOR THE REST OF THE"
 142 SCROLL
 143 PRINT "     GAME"
 150 SCROLL
 151 PRINT "-1 = GAVE UP, SITUATION HOPELESS"
 160 SCROLL
 161 PRINT " 0 = A TREMENDOUS (BUT UNFORTUN-"
 162 SCROLL
 163 PRINT "     ATELY RANDOM) LEAP"
 170 FOR P=1 TO 13
 171 SCROLL
 172 NEXT P
 173 GOTO 530
 180 DIM A(10,20)
 181 DIM B(10,20)
 182 DIM N(12)
 183 DIM L(5)
 184 DIM M(5)
 185 DIM C(5)
 186 DIM D(5)
 190 REM 
 210 FOR B=1 TO 10
 220 FOR C=1 TO 20
 230 LET X=INT (10*RND)
 240 IF X=5 THEN GOTO 270
 250 LET A(B,C)=CODE " "
 260 GOTO 280
 270 LET A(B,C)=CODE "X"
 280 NEXT C
 290 NEXT B
 300 FOR D=1 TO 10
 310 LET A(D,1)=CODE "X"
 311 LET A(D,20)=CODE "X"
 320 NEXT D
 330 FOR F=1 TO 20
 340 LET A(1,F)=CODE "X"
 341 LET A(10,F)=CODE "X"
 350 NEXT F
 360 GOTO 410
 370 LET H=INT (2+8*RND)
 380 LET I=INT (2+18*RND)
 390 IF A(H,I)<>CODE " " THEN GOTO 370
 400 RETURN
 410 GOSUB 370
 420 LET A(H,I)=CODE "*"
 430 LET J=H
 431 LET K=I
 440 FOR E=1 TO 5
 450 GOSUB 370
 460 LET A(H,I)=CODE "+"
 470 LET L(E)=H
 471 LET M(E)=I
 480 NEXT E
 490 FOR F=1 TO 10
 491 FOR G=1 TO 20
 492 LET B(F,G)=A(F,G)
 493 NEXT G
 494 NEXT F
 500 FOR F=1 TO 5
 501 LET C(F)=L(F)
 502 LET D(F)=M(F)
 503 NEXT F
 520 LET J1=J
 521 LET K1=K
 525 GOTO 100
 530 LET Y9=0
 540 PRINT AT 10,0;
 541 FOR H=1 TO 10
 550 FOR G=1 TO 20
 560 LET N$=CHR$ (A(H,G))
 570 PRINT N$;
 580 NEXT G
 590 PRINT 
 600 NEXT H
 610 IF Y9<>10 THEN GOTO 640
 630 GOTO 890
 640 PRINT AT 21,0;"YOUR COMMAND? (-1 TO 10)        "
 645 INPUT Y9
 647 PRINT AT 21,0;"                                "
 650 LET J2=J
 651 LET K2=K
 660 IF Y9=0 THEN GOTO 860
 670 IF Y9<0 THEN GOTO 1230
 680 IF Y9=10 THEN GOTO 1070
 690 IF Y9=1 THEN GOTO 820
 691 IF Y9=2 THEN GOTO 800
 692 IF Y9=3 THEN GOTO 780
 693 IF Y9=4 THEN GOTO 840
 694 IF Y9=5 THEN GOTO 890
 695 IF Y9=6 THEN GOTO 760
 696 IF Y9=7 THEN GOTO 700
 697 IF Y9=8 THEN GOTO 720
 698 IF Y9=9 THEN GOTO 740
 700 LET J=J-1
 701 LET K=K-1
 710 GOTO 890
 720 LET J=J-1
 730 GOTO 890
 740 LET J=J-1
 741 LET K=K+1
 750 GOTO 890
 760 LET K=K+1
 770 GOTO 890
 780 LET J=J+1
 781 LET K=K+1
 790 GOTO 890
 800 LET J=J+1
 810 GOTO 890
 820 LET J=J+1
 821 LET K=K-1
 830 GOTO 890
 840 LET K=K-1
 850 GOTO 890
 860 PRINT AT 21,0;"$6,000,000 JUMP."
 870 LET J=INT (2+8*RND)
 880 LET K=INT (2+18*RND)
 890 IF A(J,K)=CODE "X" THEN GOTO 1260
 900 LET A(J2,K2)=CODE " "
 910 LET A(J,K)=CODE "*"
 920 GOTO 1070
 930 REM INTERCEPTOR MOVEMENT
 940 IF A(X,Y)=CODE "X" THEN GOTO 1040
 950 LET X2=X
 951 LET Y2=Y
 960 LET X=SGN (J-X)
 961 LET Y=SGN (K-Y)
 970 LET X=X+X2
 971 LET Y=Y+Y2
 980 IF A(X,Y)=CODE "*" THEN GOTO 1050
 990 IF A(X,Y)=CODE " " THEN GOTO 1020
1000 LET A(X2,Y2)=CODE " "
1010 RETURN
1020 LET A(X,Y)=CODE "+"
1030 LET A(X2,Y2)=CODE " "
1040 RETURN
1050 LET G9=99
1060 RETURN
1070 FOR E=1 TO 5
1080 LET X=L(E)
1081 LET Y=M(E)
1090 LET G9=0
1100 GOSUB 940
1110 IF G9<>0 THEN GOTO 1240
1120 LET L(E)=X
1121 LET M(E)=Y
1130 NEXT E
1140 FOR E=1 TO 5
1150 IF A(L(E),M(E))<>CODE " " THEN GOTO 1170
1160 LET A(L(E),M(E))=CODE "+"
1170 NEXT E
1180 FOR E=1 TO 5
1190 IF A(L(E),M(E))<>CODE "X" THEN GOTO 540
1200 NEXT E
1210 SCROLL
1211 PRINT "YOU HAVE DESTROYED ALL YOUR"
1212 SCROLL
1213 PRINT "OPPONETS - THE GAME IS YOURS"
1220 GOTO 1290
1230 SCROLL
1231 PRINT "GIVE UP, EH."
1240 SCROLL
1241 PRINT "*** YOU HAVE BEEN DESTROYED BY A"
1242 SCROLL
1243 PRINT "LUCKY COMPUTER ***"
1250 GOTO 1290
1260 SCROLL
1261 PRINT "HIGH VOLTAGE."
1270 SCROLL
1271 PRINT "***** ZAP *****  YOU ARE DEAD."
1280 SCROLL
1290 SCROLL
1291 PRINT "ANOTHER GAME (Y/N)? ";
1300 INPUT A$
1301 PRINT A$
1310 IF A$<>"Y" THEN GOTO 1400
1320 SCROLL
1321 PRINT "SAME SETUP (Y/N)? ";
1330 INPUT A$
1331 PRINT A$
1340 IF A$<>"Y" THEN GOTO 92
1350 FOR F=1 TO 10
1351 FOR G=1 TO 20
1352 LET A(F,G)=B(F,G)
1353 NEXT G
1354 NEXT F
1360 FOR F=1 TO 5
1361 LET L(F)=C(F)
1362 LET M(F)=D(F)
1363 NEXT F
1380 LET J=J1
1381 LET K=K1
1385 CLS
1390 GOTO 530
1400 REM * END OF PROGRAM *
