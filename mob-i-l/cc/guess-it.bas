   1 REM ******GUESS-IT*********
   2 SCROLL
   3 PRINT TAB 11;"GUESS-IT"
   4 SCROLL
   5 PRINT TAB 6;"CREATIVE COMPUTING"
   6 SCROLL
   7 PRINT TAB 4;"MORRISTOWN, NEW JERSEY"
   8 SCROLL
   9 SCROLL
  10 SCROLL
  15 RAND 
  20 LET G1=0
  21 LET C1=0
  50 LET A1=RND
  70 LET H=5
  80 DIM P(11,11)
  90 FOR K=1 TO H
 100 LET P(K+1,1)=1
 110 LET P(1,K+1)=1/(K+1)
 120 NEXT K
 130 FOR I=1 TO H
 140 FOR J=I TO H
 150 LET P(I+1,J+1)=(1+J*P(J+1,I)*(1-P(J,I+1)))/(1+(J+2)*P(J+1,I))
 160 LET P(J+1,I+1)=(1+I*P(I+1,J)*(1-P(I,J+1)))/(1+(I+1)*P(I+1,J))
 170 NEXT J
 180 NEXT I
 190 LET Z=11
 200 DIM U(Z+1)
 201 DIM N(Z+1)
 210 SCROLL
 211 PRINT "DO YOU WANT INSTRUCTIONS? ";
 220 INPUT A$
 221 PRINT A$
 230 IF A$(1)="Y" THEN GOTO 1730
 240 IF A$(1)<>"N" THEN GOTO 210
 250 SCROLL
 260 LET G1=G1+1
 270 FOR J=2 TO Z
 280 LET U(J+1)=0
 290 NEXT J
 300 LET E=0
 301 LET T=0
 302 LET C=0
 303 LET P=0
 304 LET L=0
 310 GOSUB 1630
 330 LET D=(Z+1)
 340 SCROLL
 341 PRINT "YOUR HAND IS:"
 350 SCROLL
 360 SCROLL
 361 FOR I=H+1 TO Z-1
 370 PRINT N(I+1);"  ";
 380 NEXT I
 390 SCROLL
 400 SCROLL
 410 SCROLL
 411 PRINT "DO YOU WANT TO GO FIRST? ";
 420 INPUT A$
 421 PRINT A$
 430 IF A$(1)="Y" THEN GOTO 470
 440 IF A$(1)<>"N" THEN GOTO 390
 450 LET K=1
 460 GOTO 480
 470 LET K=0
 480 LET K=K+1
 490 LET M=H-C
 500 LET N=H-P
 510 SCROLL
 520 IF K=(INT (K/2))*2 THEN GOTO 860
 530 SCROLL
 540 SCROLL
 541 PRINT "DO YOU WANT TO GUESS THE DOWN"
 542 SCROLL
 543 PRINT "NUMBER? ";
 550 INPUT A$
 551 PRINT A$
 560 IF A$(1)="Y" THEN GOTO 1250
 570 IF A$(1)<>"N" THEN GOTO 530
 580 SCROLL
 590 SCROLL
 591 PRINT "WHAT NUMBER DO YOU WANT TO ASK"
 592 SCROLL
 593 PRINT "ABOUT? ";
 600 INPUT E
 601 PRINT E
 610 FOR I=1 TO Z
 620 IF E=U(I+1) THEN GOTO 650
 630 NEXT I
 640 GOTO 670
 650 SCROLL
 651 PRINT E;" WAS ASKED BEFORE, TRY AGAIN."
 660 GOTO 580
 670 FOR J=1 TO H
 680 IF N(J+1)=E THEN GOTO 800
 690 NEXT J
 700 SCROLL
 710 SCROLL
 711 PRINT E;" IS NOT IN MY HAND."
 720 IF M=0 THEN GOTO 1460
 730 IF N=0 THEN GOTO 1440
 740 LET Y=((M+1)*P(M+1,N)-M*P(M,N+1))/(1+(M+1)*P(M+1,N))
 750 IF RND<Y THEN GOTO 1380
 760 GOSUB 1220
 770 IF (H-P)=1 THEN GOTO 1460
 780 LET P=P+1
 790 GOTO 480
 800 SCROLL
 810 SCROLL
 811 PRINT E;" IS IN MY HAND."
 820 LET C=C+1
 830 GOSUB 1220
 840 GOTO 480
 860 IF T<>0 THEN GOTO 1410
 870 IF H-C<>0 THEN GOTO 890
 880 GOTO 1460
 890 IF H-P<>0 THEN GOTO 910
 900 GOTO 1460
 910 IF (2*H-2)-(P+C)<>0 THEN GOTO 930
 920 GOTO 1460
 940 IF RND>1/(1+(N+1)*P(N+1,M)) THEN GOTO 1060
 950 SCROLL
 960 LET A=INT (H*RND)+1
 970 FOR J=1 TO Z
 980 IF N(A+1)=U(J+1) THEN GOTO 960
 990 NEXT J
1000 SCROLL
1001 PRINT "DO YOU HAVE ";N(A+1);"? ";
1010 LET C=C+1
1020 INPUT A$
1021 PRINT A$
1030 LET E=N(A+1)
1040 GOSUB 1220
1050 GOTO 480
1060 GOSUB 1170
1070 SCROLL
1080 SCROLL
1081 PRINT "DO YOU HAVE ";N(A+1);"? ";
1090 INPUT A$
1091 PRINT A$
1100 IF A$(1)="Y" THEN GOTO 1130
1110 LET T=1
1120 GOTO 480
1130 LET E=N(A+1)
1140 LET P=P+1
1150 GOSUB 1220
1160 GOTO 480
1170 LET A=INT ((H+1)*RND)+(H+1)
1180 FOR J=1 TO Z
1190 IF N(A+1)=U(J+1) THEN GOTO 1170
1200 NEXT J
1210 RETURN
1220 LET L=L+1
1230 LET U(L+1)=E
1240 RETURN
1250 SCROLL
1260 SCROLL
1261 PRINT "WHAT DO YOU THINK THE DOWN"
1262 SCROLL
1263 PRINT "NUMBER IS? ";
1270 INPUT B
1271 PRINT B
1280 SCROLL
1290 SCROLL
1291 PRINT "THE DOWN NUMBER IS ";N(Z+1)
1300 IF B=N(Z+1) THEN GOTO 1360
1310 SCROLL
1320 SCROLL
1321 PRINT "YOUR GUESS OF ";B;" IS NOT CORRECT"
1322 SCROLL
1323 PRINT "- YOU LOSE."
1330 LET C1=C1+1
1340 GOTO 1520
1350 SCROLL
1360 SCROLL
1361 PRINT "YOUR GUESS OF ";B;" IS CORRECT"
1362 SCROLL
1363 PRINT "- YOU WIN."
1370 GOTO 1520
1380 SCROLL
1381 PRINT "I THINK YOU WHERE NOT BLUFFING"
1382 SCROLL
1383 PRINT "WHEN YOU ASKED ABOUT ";E;"."
1390 LET G=E
1400 GOTO 1480
1410 SCROLL
1411 PRINT N(A+1);" WAS NOT A BLUFF."
1420 LET G=N(A+1)
1430 GOTO 1480
1440 LET G=N(Z+1)
1450 GOTO 1480
1460 GOSUB 1170
1470 LET G=N(A+1)
1480 SCROLL
1481 PRINT "I GUESS THE DOWN NUMBER IS ";G;"."
1490 IF G=N(Z+1) THEN GOTO 1590
1500 SCROLL
1510 SCROLL
1511 PRINT "THE DOWN NUMBER IS ";N(Z+1)
1512 SCROLL
1513 PRINT "I WAS WRONG... YOU WIN."
1520 SCROLL
1521 PRINT "DO YOU WANT TO PLAY AGAIN? ";
1530 INPUT A$
1531 PRINT A$
1540 IF A$(1)="Y" THEN GOTO 250
1550 IF A$(1)<>"N" THEN GOTO 1520
1560 SCROLL
1570 SCROLL
1571 PRINT "YOU PLAYED ";G1;" GAMES."
1572 SCROLL
1573 PRINT "YOU LOST ";C1;" AND YOU WON ";G1-C1;" GAMES."
1574 SCROLL
1575 SCROLL
1576 PRINT "GOODBYE, AND TRY AGAIN LATER."
1579 CLEAR
1580 STOP
1590 SCROLL
1600 SCROLL
1601 PRINT "THE DOWN NUMBER IS ";N(Z+1)
1602 SCROLL
1603 PRINT "I WAS CORRECT...YOU LOSE."
1610 LET C1=C1+1
1620 GOTO 1520
1630 FOR I=1 TO Z
1640 LET N(I+1)=I
1650 NEXT I
1660 FOR I=1 TO Z
1670 LET R=INT (RND*((Z+1)-I))+I
1680 LET W=N(R+1)
1690 LET N(R+1)=N(I+1)
1700 LET N(I+1)=W
1710 NEXT I
1720 RETURN
1730 SCROLL
1740 SCROLL
1741 PRINT "THE OBJECT OF THIS GAME IS TO"
1742 SCROLL
1743 PRINT "GUESS AN UNKNOWN NUMBER CALLED"
1750 SCROLL
1751 PRINT "THE ""DOWN NUMBER"". THE GAME IS"
1752 SCROLL
1753 PRINT "PLAYED WITH THE NUMBERS 1 TO 11."
1760 SCROLL
1761 PRINT "YOU WILL BE GIVEN A HAND OF 5"
1770 SCROLL
1771 PRINT "RANDOMLY SELECTED NUMBERS"
1772 SCROLL
1773 PRINT "BETWEEN 1 AND 11. THE COMPUTER"
1780 SCROLL
1781 PRINT "WILL HAVE A SIMILAR HAND. THE"
1782 SCROLL
1783 PRINT "DOWN NUMBER WILL ALWAYS BE THE"
1785 SCROLL
1786 PRINT "NUMBER NOT IN EITHER PLAYERS"
1787 SCROLL
1788 PRINT "HANDS."
1790 SCROLL
1800 SCROLL
1801 PRINT "YOU ALTERNATE MOVES WITH THE"
1802 SCROLL
1803 PRINT "COMPUTER. ON ANY MOVES THERE ARE"
1810 SCROLL
1811 PRINT "TWO OPTIONS - GUESS THE DOWN"
1812 SCROLL
1813 PRINT "NUMBER OR ASK ABOUT SOME NUMBER."
1830 SCROLL
1840 SCROLL
1841 PRINT "WHEN A PLAYER GUESSES THE DOWN"
1842 SCROLL
1843 PRINT "NUMBER THE GAME STOPS."
1850 SCROLL
1851 PRINT "IF THE GUESS IS CORRECT THAT"
1852 SCROLL
1853 PRINT "PLAYER WINS. IF THE GUESS IS NOT"
1860 SCROLL
1861 PRINT "CORRECT THAT PLAYER LOSES."
1865 GOSUB 1970
1870 SCROLL
1880 SCROLL
1881 PRINT "ALL QUESTIONS ABOUT NUMBERS IN"
1882 SCROLL
1883 PRINT "THE OTHER PLAYERS HAND MUST BE"
1890 SCROLL
1891 PRINT "ANSWERED TRUTHFULLY. A PLAYER"
1892 SCROLL
1893 PRINT "MAY ""BLUFF"" BY ASKING ABOUT A"
1900 SCROLL
1901 PRINT "NUMBER IN HIS OWN HAND. THE"
1902 SCROLL
1903 PRINT "COMPUTER WILL SOMETIMES DO THIS."
1920 SCROLL
1930 SCROLL
1931 PRINT "A NUMBER MAY BE ASKED ABOUT ONLY"
1932 SCROLL
1933 PRINT "ONCE."
1940 SCROLL
1950 SCROLL
1951 PRINT "GOOD LUCK"
1960 GOTO 250
1970 POKE 16418,0
1980 PRINT AT 23,0;"PRESS A KEY, FOR A NEW PAGE  -->"
1990 PAUSE 40000
2000 PRINT AT 23,0;"                                "
2010 POKE 16418,2
2020 RETURN
2030 SAVE "GUESS-I%T"
2040 RUN 
2050 REM -----END-OF-PROGRAM----
