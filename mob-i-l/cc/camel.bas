  10 SCROLL
  11 PRINT TAB 11;"CAMEL"
  20 SCROLL
  21 PRINT TAB 5;"CREATIVE COMPUTING"
  30 SCROLL
  31 PRINT TAB 3;"MORRISTOWN, NEW JERSEY"
  35 SCROLL
  36 SCROLL
  37 SCROLL
 110 SCROLL
 111 PRINT "WOULD YOU LIKE INSTRUCTIONS? ";
 120 INPUT D$
 121 PRINT D$
 125 IF D$="" THEN GOTO 140
 130 IF D$(1)="N" THEN GOTO 320
 140 SCROLL
 141 SCROLL
 142 PRINT " WELCOME TO CAMEL. THE OBJECT IS"
 143 SCROLL
 144 PRINT "TO TRAVEL 200 MILES ACROSS THE"
 150 SCROLL
 151 PRINT "GREAT GOBI DESERT."
 160 SCROLL
 161 PRINT "A TRIBE OF KNOCKED KNEED PIGMIES"
 162 SCROLL
 163 PRINT "WILL BE CHASING YOU."
 170 SCROLL
 171 PRINT "YOU WILL BE ASKED FOR COMMANDS"
 172 SCROLL
 173 PRINT "EVERY SO OFTEN."
 180 SCROLL
 190 SCROLL
 200 SCROLL
 210 SCROLL
 211 PRINT "C O M M A N D S :"
 220 SCROLL
 221 PRINT "1. DRINK FROM YOUR CANTEEN"
 230 SCROLL
 231 PRINT "2. AHEAD MODERATE SPEED"
 240 SCROLL
 241 PRINT "3. AHEAD FULL SPEED"
 250 SCROLL
 251 PRINT "4. STOP FOR THE NIGHT"
 260 SCROLL
 261 PRINT "5. STATUS CHECK"
 270 SCROLL
 271 PRINT "6. HOPE FOR HELP"
 275 SCROLL
 276 SCROLL
 277 SCROLL
 278 SCROLL
 279 SCROLL
 280 GOSUB 2100
 281 SCROLL
 282 PRINT "YOU HAVE ONE QUART OF WATER"
 283 SCROLL
 284 PRINT "WHICH WILL LAST YOU SIX DRINKS."
 290 SCROLL
 291 PRINT "YOU MAY RENEW YOUR WATER SUPPLY"
 292 SCROLL
 293 PRINT "COMPLETELY AT AN OASES."
 300 SCROLL
 301 PRINT "YOU GET A HALF A QUART IF FOUND"
 302 SCROLL
 303 PRINT "BY HELP."
 310 SCROLL
 311 PRINT "IF HELP DOES NOT FIND YOU AFTER"
 312 SCROLL
 313 PRINT "COMMAND SIX, YOU LOSE."
 320 SCROLL
 321 PRINT "GOOD LUCK AND GOOD CAMELING."
 330 SCROLL
 331 PRINT "YOU ARE IN THE MIDDLE OF THE"
 332 SCROLL
 333 PRINT "DESERT AT AN OASES."
 335 GOSUB 2000
 340 IF C>199 THEN GOTO 1210
 350 LET Z=Z-1
 355 IF Z=1 THEN SCROLL
 356 IF Z=1 THEN PRINT "---W A R N I N G----GET A DRINK"
 360 IF Z<0 THEN GOTO 1630
 370 LET P=P+1
 380 LET X2=INT (10*RND+2.5)
 390 IF Q>0 THEN GOTO 940
 400 IF P<4 THEN GOTO 470
 410 LET C1=C1+X2
 420 IF C1<C THEN GOTO 460
 430 SCROLL
 431 PRINT "THE PYGMIES HAVE CAPTURED YOU."
 432 SCROLL
 433 PRINT "CAMEL AND PEOPLE SOUP IS THEIR"
 440 SCROLL
 441 PRINT "FAVORITE DISH."
 450 GOTO 1560
 460 SCROLL
 461 PRINT "THE PYGMIES ARE ";C-C1;" MILES"
 462 SCROLL
 463 PRINT "BEHIND YOU."
 470 SCROLL
 471 PRINT "YOU HAVE TRAVELLED ";C;" MILES"
 472 SCROLL
 473 PRINT "ALLTOGETHER."
 480 SCROLL
 481 PRINT "WHAT IS YOUR NEXT COMMAND? ";
 490 INPUT Y
 491 PRINT Y
 500 IF Y=1 THEN GOTO 830
 501 IF Y=2 THEN GOTO 610
 502 IF Y=3 THEN GOTO 680
 503 IF Y=4 THEN GOTO 760
 504 IF Y=5 THEN GOTO 790
 550 LET T=INT (10*RND)
 560 IF T<>1 THEN GOTO 1200
 570 SCROLL
 571 PRINT "HELP HAS FOUND YOU IN A STATE"
 572 SCROLL
 573 PRINT "OF UNCONSCIOUSNESS."
 580 LET S=3
 590 LET Z=4
 600 GOTO 340
 610 LET F=F+1
 620 IF F=8 THEN GOTO 1190
 630 GOSUB 880
 640 LET X1=INT (10*RND)
 650 LET C=C+X1
 660 SCROLL
 661 PRINT "YOUR CAMEL LIKES THIS PACE."
 670 GOTO 340
 680 LET F=F+3
 690 IF F>7 THEN GOTO 1190
 700 GOSUB 880
 710 LET X1=2*INT (10*RND)
 720 LET C=C+X1
 730 SCROLL
 731 PRINT "YOUR CAMEL IS BURNING ACROSS THE"
 732 SCROLL
 733 PRINT "DESERT SANDS."
 740 SCROLL
 750 GOTO 340
 760 SCROLL
 761 PRINT "YOUR CAMEL THANKS YOU."
 770 LET F=0
 780 GOTO 350
 790 SCROLL
 791 PRINT "YOUR CAMEL HAS ";7-F;" GOOD DAYS"
 792 SCROLL
 793 PRINT "LEFT."
 800 SCROLL
 801 PRINT "YOU HAVE ";S;" DRINKS LEFT IN"
 802 SCROLL
 803 PRINT "YOUR CANTEEN."
 810 SCROLL
 811 PRINT "YOU CAN GO ";Z;" COMMANDS WITHOUT"
 812 SCROLL
 813 PRINT "DRINKING."
 820 GOTO 480
 830 LET S=S-1
 840 IF S<0 THEN GOTO 1200
 850 SCROLL
 851 PRINT "BETTER WATCH FOR AN OASES."
 860 LET Z=4
 870 GOTO 480
 880 LET A=INT (100*RND)
 890 IF A>5 THEN GOTO 1120
 900 SCROLL
 901 PRINT "WILD BERBERS HIDDEN IN THE SAND"
 902 SCROLL
 903 PRINT "HAVE CAPTURED YOU."
 910 SCROLL
 911 PRINT "LUCKILY THE LOCAL SHEIK HAS"
 912 SCROLL
 913 PRINT "AGREED TO THEIR RANSOMDEMANDS..."
 920 SCROLL
 921 PRINT "....BUT...WATCH FOR THE PYGMIES."
 930 SCROLL
 931 PRINT "YOU HAVE A NEW CHOICE OF"
 932 SCROLL
 933 PRINT "SUB-COMMANDS:"
 940 SCROLL
 941 PRINT "7. ATTEMPT AN ESCAPE"
 950 SCROLL
 951 PRINT "8. WAIT FOR PAYMENT"
 960 SCROLL
 961 PRINT "YOUR SUB-COMMAND: ";
 970 INPUT X
 971 PRINT X
 980 IF X=8 THEN GOTO 1060
 990 LET X1=INT (10*RND)
1000 IF X1<5 THEN GOTO 1040
1010 SCROLL
1011 PRINT "CONGRATULATIONS, YOU"
1012 SCROLL
1013 PRINT "SUCCESSFULLY ESCAPED."
1020 LET Q=0
1030 GOTO 340
1040 SCROLL
1041 PRINT "YOU WERE MORTALLY WOUNDED BY A"
1042 SCROLL
1043 PRINT "PIG STABBER WHILE ESCAPING."
1050 GOTO 1410
1060 LET X1=INT (100*RND)
1070 REM 
1080 IF X1>24 THEN GOTO 1100
1090 SCROLL
1091 PRINT "YOUR RANSOM HAS BEEN PAID AND"
1092 SCROLL
1093 PRINT "YOU ARE FREE TO GO."
1095 LET Q=0
1096 GOTO 340
1100 SCROLL
1101 PRINT "THE LOCAL SULTAN IS COLLECTING.."
1102 SCROLL
1103 PRINT "......JUST WAIT......."
1110 GOTO 340
1120 LET A=INT (10*RND)
1130 IF A>2 THEN GOTO 1240
1140 SCROLL
1141 PRINT "YOU HAVE ARRIVED AT AN OASES----"
1142 SCROLL
1143 PRINT "------YOUR CAMEL IS FILLING YOUR"
1150 SCROLL
1151 PRINT "CANTEEN AND EATING FIGS."
1160 LET Z=4
1170 LET S=6
1180 RETURN
1190 SCROLL
1191 PRINT "YOU DIRTY RAPSCALLION. YOU RAN"
1192 SCROLL
1193 PRINT "YOUR POOR CAMEL TO DEATH."
1200 GOTO 1410
1210 SCROLL
1211 PRINT "YOU WIN, A PARTY IS BEING GIVEN"
1212 SCROLL
1213 PRINT "IN YOUR HONOR......."
1220 SCROLL
1221 PRINT "........THE PYGMIES ARE PLANNING"
1222 SCROLL
1223 PRINT "TO ATTEND......."
1230 GOTO 1560
1240 LET X1=INT (100*RND)
1250 IF X1>5 THEN GOTO 1350
1260 SCROLL
1261 PRINT "YOU HAVE BEEN CAUGHT IN A"
1262 SCROLL
1263 PRINT "SANDSTORM.....GOOD LUCK."
1270 LET X5=INT (10*RND)
1280 LET X6=INT (10*RND)
1290 IF X6<5 THEN GOTO 1320
1300 LET C=C+X5
1310 GOTO 1330
1320 LET C=C-X5
1330 SCROLL
1331 PRINT "YOUR NEW POSITION IS ";C;" MILES"
1332 SCROLL
1333 PRINT "SO FAR."
1340 RETURN
1350 LET X1=INT (100*RND)
1360 IF X1>5 THEN RETURN
1370 LET C1=C1+1
1380 SCROLL
1381 PRINT "YOUR CAMEL HURT HIS HUMP."
1390 SCROLL
1391 PRINT "LUCKILY THE PYGMIES WERE"
1392 SCROLL
1393 PRINT "FOOTWEARY."
1400 RETURN
1410 LET U=INT (10*RND)
1420 SCROLL
1421 PRINT "YOU DIED IN THE DESERT."
1430 IF U>1 THEN GOTO 1460
1440 SCROLL
1441 PRINT "THE NATIONAL CAMEL""S UNION IS"
1442 SCROLL
1443 PRINT "NOT ATTENDING YOUR FUNERAL."
1450 GOTO 1560
1460 IF U>3 THEN GOTO 1490
1470 SCROLL
1471 PRINT "YOUR BODY WAS EATEN BY VULTURES"
1472 SCROLL
1473 PRINT "AND IMPORTED CANNIBALS."
1480 GOTO 1560
1490 IF U>5 THEN GOTO 1520
1500 SCROLL
1501 PRINT "THE LOCAL SHEIK NOW USES YOUR"
1502 SCROLL
1503 PRINT "SKULL FOR A CHANGE PURSE."
1510 GOTO 1560
1520 IF U>7 THEN GOTO 1550
1530 SCROLL
1531 PRINT "PEOPLE WITH LITTLE INTELLIGENCE"
1532 SCROLL
1533 PRINT "SHOULD STAY OUT OF THE DESERT."
1540 GOTO 1560
1550 SCROLL
1551 PRINT "TURKEYS SHOULD FLY, NOT RIDE"
1552 SCROLL
1553 PRINT "CAMELS."
1560 SCROLL
1570 SCROLL
1580 SCROLL
1581 PRINT "WANT A NEW CAMEL AND NEW GAME? ";
1590 INPUT D$
1591 PRINT D$(1)
1600 IF D$(1)="Y" THEN GOTO 320
1620 GOTO 1650
1630 SCROLL
1631 PRINT "YOU RAN OUT OF WATER......"
1632 SCROLL
1633 PRINT ".....SORRY CHUM."
1640 GOTO 1410
1650 SCROLL
1651 PRINT "-----------------"
1654 SCROLL
1655 PRINT "     CHICKEN"
1656 SCROLL
1657 PRINT "-----------------"
1660 STOP
2000 LET Z=4
2010 LET S=6
2020 LET C=0
2030 LET C1=0
2040 LET Q=0
2050 LET F=0
2060 LET P=0
2070 RETURN
2100 POKE 16418,0
2110 PRINT AT 23,0;"* PRESS A KEY, FOR A NEW PAGE"
2120 IF INKEY$ <>"" THEN GOTO 2120
2130 IF INKEY$ ="" THEN GOTO 2130
2135 PRINT AT 23,0;"                                "
2140 POKE 16418,2
2150 RETURN
2200 SAVE "CAME%L"
2210 RUN 
