  10 REM DEMONSTRATION FOR SIN X
  20 SLOW
  30 DIM A(6)
  40 LET A(1)=-.000000003
  50 LET A(2)=0.000000592
  60 LET A(3)=-.000068294
  70 LET A(4)=0.004559008
  80 LET A(5)=-.142630785
  90 LET A(6)=1.276278962
 100 PRINT 
 110 PRINT "ENTER START VALUE IN DEGREES"
 120 INPUT C
 130 CLS
 140 LET C=C-10
 150 PRINT "BASIC PROGRAM","ROM PROGRAM"
 160 PRINT "-------------","-----------"
 170 PRINT 
 180 FOR J=1 TO 4
 190 LET C=C+10
 200 LET Y=C/360-INT (C/360+.5)
 210 LET W=4*Y
 220 IF W>1 THEN LET W=2-W
 230 IF W<-1 THEN LET W=-W-2
 240 LET Z=2*W*W-1
 250 LET BERG=6
 260 REM USE "SERIES GENERATOR"
 270 GOSUB 550
 280 PRINT TAB 6;"SIN ";C;" DEGREES"
 290 PRINT 
 300 PRINT T*W,SIN (PI*C/180)
 310 PRINT 
 320 NEXT J
 330 GOTO 100
 500 REM SERIES GENERATOR,ENTER
 510 REM USING THE COUNTER BERG
 520 REM AND ARRAY-A HOLDING THE
 530 REM CONSTANTS.
 540 REM FIRST VALUE IN Z.
 550 LET M0=2*Z
 560 LET M2=0
 570 LET T=0
 580 FOR I=BERG TO 1 STEP -1
 590 LET M1=M2
 600 LET U=T*M0-M2+A(BERG+1-I)
 610 LET M2=T
 620 LET T=U
 630 NEXT I
 640 LET T=T-M1
 650 RETURN
