  10 SLOW
  20 CLS
  30 PRINT "REGISTER HUVUD-MENY"
  40 PRINT "-------------------"
  50 PRINT 
  60 PRINT "1 NY-UPPLAGGNING"
  70 PRINT "2 UPP-DATERING"
  80 PRINT "3 S0KNING"
  90 PRINT "4 SORTERING"
  95 PRINT "5 REGISTER UTSKRIFT"
 100 PRINT "6 SLUT (INSPELNING)"
 110 PRINT 
 120 PRINT "ANGE RUTIN GENOM ATT ANGE RUTIN-NUMMER"
 130 GOSUB 400
 140 IF I=6 THEN GOTO 500
 150 IF NOT (I>0 AND I<7) THEN LET I=0.3
 160 GOSUB I*1000
 170 GOTO 10
 300 FOR L=1 TO 5
 310 CLS
 320 PAUSE 5
 330 PRINT "FELAKTIG INMATNING"
 340 PAUSE 10
 350 NEXT L
 360 RETURN
 400 PAUSE 40000
 410 LET I=VAL INKEY$ 
 420 RETURN
 500 CLS
 510 PRINT "SATT BANDSPELAREN PA INSPELNING.TRYCK SEDAN PA I-TANGENTEN."
 520 GOSUB 600
 525 IF NOT I$="I" THEN GOTO 540
 530 SAVE "REGISTE%R"
 540 GOTO 10
 605 IF INKEY$ <>"" THEN GOTO 605
 610 IF INKEY$ ="" THEN GOTO 610
 620 LET I$=INKEY$ 
 630 RETURN
1000 CLS
1010 PRINT "NY-UPPLAGGNING"
1020 PRINT "--------------"
1030 PRINT 
1040 PRINT "ANTAL POSTER:";
1050 INPUT PO
1060 PRINT PO;" ST"
1070 PRINT "ANTAL TERMER/POST:";
1080 INPUT TE
1090 PRINT TE;" ST"
1100 LET T=PO*TE
1110 PRINT "ANTAL TECKEN/TERM:";
1120 INPUT LE
1130 PRINT LE;" ST"
1135 DIM Z$(TE,LE)
1140 LET X=PO*TE*(LE+2)
1150 IF X<11050 THEN GOTO 1200
1170 GOSUB 300
1180 GOTO 1000
1200 CLS
1210 PRINT 11050-X;" BYTE KVAR I MINNET."
1215 PRINT "SKA REGISTRET VARA ST0RRE (J/N)"
1220 GOSUB 600
1230 IF I$="J" THEN GOTO 1000
1240 DIM R$(1+PO*TE,LE)
1250 LET C=0
1260 CLS
1270 DIM N$(TE,10)
1280 FOR Z=1 TO TE
1290 PRINT "GE DE ";TE;" TERMERNA NAMN"
1300 PRINT "---------------------"
1310 FOR L=1 TO TE
1320 PRINT L;": ";N$(L)
1330 NEXT L
1340 PRINT 
1350 PRINT "(MAX 10 TECKEN/NAMN)"
1360 INPUT N$(Z)
1365 CLS
1370 NEXT Z
1380 PRINT "UPPLAGGNINGEN AR KLAR OCH INMAT-NINGEN AV REGISTRET STAR PA TUR."
1390 PAUSE 300
1400 GOSUB 2000
1410 RETURN
2000 CLS
2010 PRINT "UPPDATERINGS-RUTIN"
2020 PRINT "------------------"
2030 PRINT 
2040 PRINT "1 NY-TILLAGG"
2050 PRINT "2 ANDRING"
2060 PRINT "3 SLUT"
2065 PRINT 
2070 PRINT "ANGE RUTIN GENOM ATT ANGE RUTIN-NUMMER"
2075 GOSUB 400
2080 IF I=3 THEN RETURN
2085 IF NOT (I>0 AND I<3) THEN LET I=4
2090 GOSUB I*100+2000
2095 GOTO 2000
2100 CLS
2105 PRINT "REGISTRET AR DIMENSIONERAT FOR",PO;" POSTER."
2110 PRINT "DET INNEHALLER NU ";C;" POSTER."
2115 PRINT 
2120 PRINT "ANGE DET ANTAL POSTER SOM DU NU VILL MATA IN:"
2125 INPUT P
2130 IF P+C-1<=PO THEN GOTO 2148
2135 CLS
2140 PRINT "DETTA FAR EJ PLATS"
2145 GOTO 2105
2148 LET P=P+C
2150 LET C=C+1
2151 FOR L=C TO P
2152 FOR X=1 TO TE
2153 CLS
2155 FOR Y=1 TO TE
2158 PRINT N$(Y);":";Z$(Y)
2160 NEXT Y
2162 PRINT AT X-1,11;"\##"
2164 INPUT Z$(X)
2166 IF LEN Z$(X)<=LE THEN GOTO 2172
2168 PRINT "F0R LANG, F0RS0K IGEN."
2170 GOTO 2164
2172 NEXT X
2174 FOR Y=1 TO TE
2176 LET R$(PO*(Y-1)+L)=Z$(Y)
2178 LET Z$(Y)=""
2180 NEXT Y
2190 NEXT L
2195 LET C=P
2197 CLS
2199 RETURN
2300 CLS
2305 PRINT "ANDRINGS-RUTIN"
2310 PRINT "--------------"
2315 PRINT 
2320 PRINT "OM DU INTE VET VILKET POSTNUMMERDU SKA ANDRA KAN DU ANVANDA S0K-RUTINEN TILL ATT TA REDA PA DET"
2325 PRINT 
2330 PRINT "VILL DU HA S0KRUTINEN (J/N)?"
2335 GOSUB 600
2340 IF I$="J" THEN GOSUB 3000
2345 CLS
2350 PRINT "ANGE POST-NUMMER"
2355 INPUT I
2358 CLS
2360 FOR X=1 TO TE
2365 PRINT X;" ";N$(X);" ";R$(PO*(X-1)+I)
2370 NEXT X
2371 PRINT 
2373 PRINT "VILKEN TERM SKA ANDRAS ?"
2374 PRINT "FOR SLUT-ANGE 0"
2376 INPUT S
2378 IF S=0 THEN GOTO 2388
2380 PRINT "GE NYTT INNEHALL"
2382 INPUT I$
2384 LET R$(PO*(S-1)+I)=I$
2386 GOTO 2358
2388 CLS
2390 PRINT "FLER POSTER (J/N)?"
2392 GOSUB 600
2394 IF I$="J" THEN GOTO 2300
2399 RETURN
2400 GOSUB 300
2410 RETURN
3000 CLS
3010 PRINT "S0K-RUTIN"
3020 PRINT "---------"
3030 PRINT 
3040 FOR L=1 TO TE
3050 PRINT L;" ";N$(L)
3060 NEXT L
3070 PRINT 
3080 PRINT "ANGE S0K-BEGREPP (1-";TE;")"
3090 GOSUB 400
3100 IF (I>0 AND I<=TE) THEN GOTO 3130
3110 GOSUB 300
3120 GOTO 3010
3130 LET S=I
3133 CLS
3135 PRINT N$(I)
3137 PRINT "----------"
3140 PRINT 
3150 PRINT "ANGE ORD SOM SKA S0KAS"
3160 PRINT 
3170 PRINT "OBS. S0KNING SKER ENDAST MED DET     ANTAL TECKEN SOM ANGES"
3175 INPUT J$
3180 LET I=LEN J$
3183 CLS
3185 FAST
3190 LET X=1+PO*(S-1)
3195 LET W=X+PO
3197 LET S=0
3200 FOR L=X TO W
3205 IF S<0 THEN GOTO 3220
3210 IF J$=R$(L)(1 TO I) THEN GOSUB 3400
3220 NEXT L
3225 SLOW
3230 IF S=0 THEN PRINT "FINNS EJ I REGISTRET"
3300 PRINT "S0KNINGEN KLAR"
3310 PRINT "FLER S0KNINGAR (J/N)?"
3320 GOSUB 600
3330 IF I$="J" THEN GOTO 3000
3340 RETURN
3400 LET S=L+1-X
3410 SLOW
3420 PRINT "POST NR:";S
3430 PRINT "-----------"
3440 FOR Y=1 TO TE
3450 PRINT N$(Y);":";R$(PO*(Y-1)+S)
3460 NEXT Y
3462 PRINT 
3464 PRINT "VILL DU HA UTSKRIFT (J/N)?"
3466 GOSUB 600
3468 IF I$="J" THEN COPY
3470 PRINT 
3480 PRINT "SKA S0KNINGEN FORTSATTA (J/N)?"
3490 GOSUB 600
3500 IF I$<>"J" THEN LET S=-1
3505 CLS
3507 FAST
3510 RETURN
4000 CLS
4005 PRINT "SORTERINGS-RUTIN"
4010 PRINT "----------------"
4015 PRINT 
4020 FOR X=1 TO TE
4025 PRINT X;" ";N$(X)
4028 NEXT X
4030 PRINT 
4035 PRINT "ANGE SORTERINGS-TERM (1-";TE;")"
4038 INPUT I
4040 CLS
4045 LET X=1+PO*(I-1)
4046 LET F=X-1
4048 LET S=X-1+PO
4050 LET A=Y-1
4052 FAST
4054 FOR L=X TO S-1
4055 FOR W=L+1 TO S
4056 IF R$(W)(1)=" " THEN LET R$(W)="%$"
4057 IF R$(L)<R$(W) THEN GOTO 4090
4060 FOR R=0 TO TE-1
4062 LET Q=L-F+R*PO
4064 LET E=W-F+R*PO
4066 LET Z$(1)=R$(Q)
4068 LET R$(Q)=R$(E)
4070 LET R$(E)=Z$(1)
4075 NEXT R
4090 NEXT W
4095 NEXT L
4099 RETURN
6000 CLS
6005 PRINT "UTSKRIFTS-RUTIN"
6010 PRINT "---------------"
6020 PRINT 
6022 PRINT "VILL DU SE POSTERNA (J/N)?"
6024 GOSUB 600
6027 IF I$="J" THEN PRINT "DE SKICKAS TILL PRINTERN MED NEWLINE-TANGENTEN"
6030 PRINT 
6035 PRINT "ANGE F0RSTA UTSKRIFTS-POST:";
6040 INPUT X
6050 PRINT X
6060 PRINT "ANGE SISTA UTSKRIFTS-POST:";
6070 INPUT Y
6075 FAST
6080 FOR L=X TO Y
6085 CLS
6090 FOR R=0 TO TE-1
6100 PRINT R$(L+PO*R)
6110 NEXT R
6120 IF I$="J" THEN PAUSE 40000
6125 COPY
6130 NEXT L
6140 RETURN
7000 INPUT I
7005 CLS
7010 FOR L=I TO I+20
7020 PRINT L,PEEK L
7030 NEXT L
7040 GOTO 7000
