   5 REM POLYDIV,
   7 REM BASED ON A ROUTINE FOR TI-83 BY TIMOTHY FOSTER OF TIBASICDEV.
  10 LET O=PI-PI
  11 LET I=PI/PI
  12 LET Z=I+I
  13 LET E=Z+I
  14 LET T=20
  19 LET K=1E3
  20 DIM L$(Z,Z**E)
  30 LET L$(I)="DIVIDEND"
  40 LET L$(Z)="DIVISOR"
  50 DIM D(E)
 110 LET J=I
 115 GOSUB Z*K
 130 LET J=Z
 135 GOSUB Z*K
 140 DIM L(E,D(I))
 150 LET J=I
 160 GOSUB I*K
 170 LET J=Z
 180 GOSUB I*K
 200 LET D(E)=D(I)-D(Z)+I
 210 FOR A=I TO D(E)
 220 LET L(E,A)=L(I,A)/L(Z,I)
 230 FOR B=Z TO D(Z)
 250 LET L(I,A+B-I)=L(I,A+B-I)-L(E,A)*L(Z,B)
 260 NEXT B
 270 NEXT A
 300 CLS
 400 PRINT "FIRST TERM:";
 410 PRINT " X**";D(I)-D(Z)
 415 PRINT "QUOTIENT:";
 420 FOR A=I TO D(E)
 430 PRINT " ";L(E,A);
 440 NEXT A
 443 PRINT 
 445 PRINT "REMAINDER:";
 450 FOR A=D(I)-D(Z)+Z TO D(I)
 460 PRINT " ";L(I,A);
 470 NEXT A
 999 STOP
1000 CLS
1005 PRINT AT T,O;L$(J)
1010 FOR A=I TO D(J)
1020 PRINT AT T+I,O;"COEFFICIENT OF X**";D(J)-A;"? "
1030 INPUT L(J,A)
1040 NEXT A
1050 RETURN
2000 PRINT AT T+I,O;"DEGREE OF ";L$(J);"?"
2010 INPUT D(J)
2015 LET D(J)=D(J)+I
2020 RETURN
